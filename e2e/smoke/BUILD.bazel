load("@bazel_lib//lib:bats.bzl", "bats_test")
load("@bazel_lib//lib:copy_directory.bzl", "copy_directory")
load("@bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@bazel_lib//lib:diff_test.bzl", "diff_test")
load("@bazel_lib//lib:expand_template.bzl", "expand_template")
load("@io_bazel_rules_go//go:def.bzl", "go_test")

# Validate that copy_directory works and resolves its toolchain
copy_directory(
    name = "copy_directory_case",
    src = "d",
    out = "d_out",
    verbose = True,
)

diff_test(
    name = "copy_directory_test",
    file1 = "d",
    file2 = "copy_directory_case",
)

# Validate that copy_to_directory works and resolves its toolchain
copy_to_directory(
    name = "copy_to_directory_case",
    srcs = ["d"],
    out = "d2_out",
    replace_prefixes = {"d/": ""},
)

diff_test(
    name = "copy_to_directory_test",
    file1 = "d",
    file2 = "copy_to_directory_case",
)

# Validate that expand_template works and resolves its toolchain
expand_template(
    name = "a_tmpl_stamp",
    out = "a_stamp",
    data = ["a.tmpl"],
    stamp = 1,
    stamp_substitutions = {
        "{{VERSION}}": "v{{BUILD_SCM_VERSION}}",
    },
    substitutions = {
        "{{VERSION}}": "v0.0.0",
        "{{WORKSPACE}}": "$(WORKSPACE)",
    },
    template = "a.tmpl",
)

diff_test(
    name = "a_stamp_test",
    file1 = ":a_stamp",
    file2 = select({
        "@bazel_lib//lib:bzlmod": "a_stamp_expected_bzlmod",
        "//conditions:default": "a_stamp_expected",
    }),
)

bats_test(
    name = "bats",
    srcs = [
        "basic.bats",
    ],
)

copy_to_directory(
    name = "copy_to_directory_mtime_case",
    srcs = ["d"],
    out = "copy_to_directory_mtime_out",
    preserve_mtime = True,
    tags = ["no-remote"],
)

copy_directory(
    name = "copy_directory_mtime_case",
    src = "d",
    out = "copy_directory_mtime_out",
    preserve_mtime = True,
    tags = ["no-remote"],
)

go_test(
    name = "test_preserve_mtime",
    size = "small",
    srcs = ["test_preserve_mtime.go"],
    data = [
        "d",
        ":copy_directory_mtime_case",
        ":copy_to_directory_mtime_case",
    ],
    # Note: This test is marked as manual as it is not reliable in the CI environment. The reason for is that changes
    # to only the modify time do not invalidate the Bazel cache. These failures can be reproduced by:
    #   bazel clean --expunge
    #   bazel test :test_preserve_mtime # This test should pass
    #   touch d/1 # Update the mtime, in the CI environment this is done with the SCM integration.
    #   bazel test :test_preserve_mtime # This test now fails
    tags = [
        "external",
        "manual",
        "no-remote",
    ],
    deps = [
        "@io_bazel_rules_go//go/tools/bazel:go_default_library",
    ],
)
